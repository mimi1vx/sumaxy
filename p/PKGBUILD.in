# Maintainer: Roman Neuhauser <neuhauser+sumaxy@suse.cz>
pkgname=sumaxy
pkgver=__VERSION__
pkgrel=1
pkgdesc="Extract repository URLs from OBS metadata for maintenance updates"
arch=(any)
url="https://github.com/roman-neuhauser/$pkgname"
license=(MIT)
depends=(haveopt xmlstarlet zsh)
checkdepends=(cram haveopt xmlstarlet zsh)
source=($pkgname-$pkgver.tar.gz)
sha1sums=(SKIP)

build()
{
  _in_src ./configure --prefix=/usr
  _in_src make
}

check()
{
  _in_src make check
}

package()
{
  _in_src make install DESTDIR="$pkgdir/"
  sed -i s:dl.example.org:download.suse.de: "$pkgdir"/etc/sumaxy
}

_in_src()
(
  cd "$srcdir/$pkgname-$pkgver"
  "$@"
)
